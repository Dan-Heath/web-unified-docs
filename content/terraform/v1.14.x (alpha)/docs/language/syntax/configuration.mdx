---
page_title: Syntax - Configuration Language
description: >-
  Key constructs of the native Terraform language syntax, including identifiers,
  arguments, blocks, and comments.
---

# Configuration syntax

This page describes the basic syntax elements of the Terraform language: blocks, arguments, identifiers, and comments.

Terraform uses _HCL_ (HashiCorp Configuration Language), which is also used by other HashiCorp products. You can also express Terraform language constructs in [JSON syntax](/terraform/language/syntax/json), which is harder for humans to read and edit but easier to generate and parse programmatically.

For complete HCL syntax details, refer to the [HCL native syntax specification](https://github.com/hashicorp/hcl/blob/main/hclsyntax/spec.md).

## Identifiers

Identifiers are names for things in Terraform: block types, argument names, resource names, variable names, etc.

Identifiers can contain letters, digits, underscores (`_`), and hyphens (`-`). The first character cannot be a digit.

<CodeBlockConfig hideClipboard>

```hcl
resource "aws_instance" "web-server" {
  instance_type = "t3.micro"
}

variable "vpc_cidr" {
  type = string
}
```

</CodeBlockConfig>

Terraform implements [the Unicode identifier syntax](http://unicode.org/reports/tr31/), extended to include the hyphen character `-`.

## Blocks

Blocks are containers that group related configuration together. Every block has a type and can contain arguments and nested blocks.

| Block type | Identifiers | Purpose | Example |
|------------|-------------|---------|---------|
| `locals` | 0 | Define local values | `locals { }` |
| `variable` | 1 | Define input variables | `variable "instance_type" { }` |
| `output` | 1 | Define output values | `output "instance_ip" { }` |
| `resource` | 2 | Define infrastructure | `resource "aws_instance" "web" { }` |
| `data` | 2 | Query existing data | `data "aws_ami" "ubuntu" { }` |

### Resource blocks

Resource blocks create infrastructure. They require a resource type and a resource name.

The following example shows a resource block of type `aws_instance` and name `web`:

<CodeBlockConfig hideClipboard>

```hcl
resource "aws_instance" "web" {
  ami           = "ami-0c02fb55956c7d316"
  instance_type = "t3.micro"
}
```

</CodeBlockConfig>

### Data blocks

Data blocks query existing infrastructure. They also require a data source type and a data source name.

The following example shows a data block of type `aws_ami` and name `ubuntu`:

<CodeBlockConfig hideClipboard>

```hcl
data "aws_ami" "ubuntu" {
  most_recent = true
  owners      = ["099720109477"]
}
```

</CodeBlockConfig>

### Variable and output blocks

Variable and output blocks require a name.

The following example shows a variable block of name `instance_type` and an output block of name `instance_ip`:

<CodeBlockConfig hideClipboard>

```hcl
variable "instance_type" {
  type        = string
  description = "EC2 instance type"
}

output "instance_ip" {
  value = aws_instance.web.public_ip
}
```

</CodeBlockConfig>

### Local blocks

Local blocks define local values. Local values have no block-level identifiers. You specify the local values as arguments in the block. The following example shows a local value for `instance_type`:

<CodeBlockConfig hideClipboard>

```hcl
locals {
  instance_type = "t3.micro"
}
```

</CodeBlockConfig>

## Arguments

Arguments assign values to names within blocks.

<CodeBlockConfig hideClipboard>

```hcl
resource "aws_instance" "web" {
  ami           = "ami-0c02fb55956c7d316"
  instance_type = locals.instance_type
}
```

</CodeBlockConfig>

The identifier before the equals sign is the _argument name_. The expression after the equals sign is the argument's value.

Many arguments accept [expressions](/terraform/language/reference/expressions), which let you specify values directly or generate them from other values.

<Note>
  
HCL documentation uses "attribute" instead of "argument." These terms are interchangeable. Terraform uses "argument" to avoid confusion with resource attributes like `id` that you can reference but cannot assign.

</Note>

## Comments

Terraform supports three comment syntaxes:

1. `#` begins a single-line comment
1. `//` also begins a single-line comment  
1. `/*` and `*/` delimit multi-line comments

The following example shows all three comment syntaxes:

<CodeBlockConfig hideClipboard>

```hcl
# This is a single-line comment
resource "aws_instance" "web" {
  ami = "ami-0c02fb55956c7d316"  // This is also a single-line comment
  
  /* This is a
     multi-line comment */
  instance_type = "t3.micro"
}
```

</CodeBlockConfig>

The `#` single-line comment style is the default comment style and should be used in most cases. Formatting tools may convert `//` comments to `#` comments.

## Character encoding and line endings

Terraform configuration files must be UTF-8 encoded. Terraform accepts non-ASCII characters in identifiers, comments, and string values.

Terraform accepts Unix-style (LF) or Windows-style (CRLF) line endings. Use Unix-style (LF) line endings. Formatting tools may convert CRLF to LF.