---
page_title: Hide sensitive values with the sensitive argument
description: Use the sensitive argument to redact values from Terraform CLI output and UI while storing them in state.
---

# Hide sensitive values with the sensitive argument

Use the `sensitive` argument on `variable` and `output` blocks when you want to redact those values from Terraform CLI log output and the HCP Terraform UI. Terraform also treats any expressions that reference a sensitive variable or output as inherently sensitive.

> **Hands-on:** Try the [Protect Sensitive Input Variables](/terraform/tutorials/configuration-language/sensitive-variables) tutorial.

## Add sensitive to variables

Add the `sensitive` argument to `variable` blocks that contain sensitive data:

<CodeBlockConfig highlight="4">

```hcl
variable "database_password" {
  description = "Password for the database instance"
  type        = string
  sensitive   = true
}
```

</CodeBlockConfig>

## Add sensitive to outputs

Add `sensitive` to `output` blocks that contain sensitive data:

<CodeBlockConfig highlight="4">

```hcl
output "connection_string" {
  description = "Database connection string"
  value       = "postgresql://${var.db_username}:${var.database_password}@${aws_db_instance.main.endpoint}/mydb"
  sensitive   = true
}
```

</CodeBlockConfig>

## Automatic sensitivity propagation

Terraform automatically treats any expression that references a sensitive variable or output as sensitive. In the following example, the `password` argument references the sensitive `var.database_password`:

<CodeBlockConfig highlight="5">

```hcl
resource "aws_db_instance" "main" {
  identifier = "my-database"
  engine     = "postgres"
  username   = var.db_username
  password   = var.database_password
}
```

</CodeBlockConfig>

Terraform redacts the `password` argument from the `main` resource in plan and apply operations:

<CodeBlockConfig highlight="3">

```shell-session
# aws_db_instance.main will be created
+ resource "aws_db_instance" "main" {
    + password = (sensitive value)
    + username = "admin"
  }
```

</CodeBlockConfig>

## CLI output behavior

Terraform redacts sensitive variables and outputs in the operation log. For example, the `connection_string` output appears as:

```shell-session
Apply complete! Resources: 1 added, 0 changed, 0 destroyed.

Outputs:

connection_string = (sensitive value)
```

## Important limitations

Terraform stores values with the `sensitive` argument in both state and plan files. Anyone who can access those files can access your sensitive values.

If you use the [`terraform output` CLI command](/terraform/cli/commands/output) with the `-json` or `-raw` flags, Terraform displays sensitive variables and outputs in plain text.

If you do not want to store the values of your sensitive variables and outputs, refer to [Ephemeral values](/terraform/language/manage-sensitive-data/ephemeral). If you have sensitive values in your state and plan files, we recommend [securing your state](/terraform/language/manage-sensitive-data/state-security).
