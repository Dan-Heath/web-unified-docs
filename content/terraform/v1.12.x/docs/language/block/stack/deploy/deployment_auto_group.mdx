---
page_title: Deployment auto approve block reference for Terraform Stacks
description: Use the `deployment_auto_approve` block to define rules that automatically approve deployment plans based on specific conditions.
---

# `deployment_auto_approve` block reference

Use the `deployment_auto_approve` block to define rules that automatically approve deployment plans based on specific conditions.

## Background

The `deployment_auto_approve` block defines rules that automatically approve deployment plans when specific conditions are met. These rules help automate your deployment workflow by removing the need for manual approval when changes meet your safety criteria.

HCP Terraform evaluates the `check` blocks within your `deployment_auto_approve` block to determine if it should approve the plan automatically. If all checks pass, HCP Terraform approves the plan for you. If one or more conditions do not pass, HCP Terraform shows the reason why and requires manual approval.

By default, each Stack has a `deployment_auto_approve` rule named `empty_plan`, which automatically approves a plan if it contains no changes.

## Configuration model

The `deployment_auto_approve` block supports the following arguments:

- [`deployment_auto_approve "<LABEL>"`](#deployment_auto_approve) &nbsp; block
  - [`check`](#check) &nbsp; block
    - [`condition`](#condition) &nbsp; expression
    - [`reason`](#reason) &nbsp; string

## Complete configuration

All available arguments are defined in the following `deployment_auto_approve` block:

```hcl
deployment_auto_approve "<LABEL>" {
  check {
    condition = <EXPRESSION>
    reason    = "<ERROR_MESSAGE>"
  }
}
```

## Specification

A `deployment_auto_approve` block supports the following configuration.

### `deployment_auto_approve "<LABEL>"`

The `deployment_auto_approve` block requires a label that serves as a unique name for the auto-approval rule within the Stack.

The following arguments are supported in a `deployment_auto_approve` block:

| Argument | Description | Type | Required? |
| --- | --- | --- | --- |
| `check` | A block containing conditions and reasons for the orchestration rule. | Block | Required |

### `check`

The `check` block contains the conditions that must be met for the auto-approval rule to execute. All checks within a `deployment_auto_approve` block must pass for the rule to trigger.

```hcl
deployment_auto_approve "safe_changes" {
  check {
    condition = context.plan.changes.total < 5
    reason    = "Plan contains too many changes for auto-approval."
  }
}
```

The following arguments are supported in a `check` block:

| Argument | Description | Type | Required? |
| --- | --- | --- | --- |
| `condition` | An expression that determines whether the check passes. | Expression | Required |
| `reason` | A message explaining why the check failed, shown in HCP Terraform when manual approval is required. | String | Required |

### `condition`

The `condition` argument contains an expression that must evaluate to `true` for the check to pass. The condition has access to a `context` variable that includes information about the current deployment plan.

```hcl
check {
  condition = context.plan.component_changes["component.database"].total == 0
  reason    = "Database component has changes that require manual review."
}
```

#### Summary

- Data type: Expression
- Default: None
- Required: Yes

### `reason`

The `reason` argument provides a human-readable message that explains why the check failed. HCP Terraform displays this message when the condition evaluates to `false` and manual approval is required.

```hcl
check {
  condition = context.success == true
  reason    = "Operation failed and requires manual intervention."
}
```

#### Summary

- Data type: String
- Default: None
- Required: Yes

## Orchestration context

A `check` block's `condition` field has access to a `context` variable, which includes information about the current deployment plan. The `context` variable contains the following fields:

| Field | Description | Type |
| --- | --- | --- |
| `operation` | The current operation HCP Terraform is performing. | String ("plan" or "apply") |
| `success` | Indicates whether the operation succeeded. | Boolean |
| `plan` | An object including data about the current plan. | Object |
| `errors` | A set of diagnostic error message objects. | Set of objects |
| `warnings` | A set of diagnostic warning message objects. | Set of objects |

### Plan context

The `context.plan` field returns an object with the following fields:

| Field | Description | Type |
| --- | --- | --- |
| `mode` | The plan mode. | String ("normal", "refresh-only", or "destroy") |
| `applyable` | Whether the plan can be applied. | Boolean |
| `changes` | A change summary object. | Object |
| `component_changes` | A map of change summary objects for each component instance. | Map of objects |
| `replans` | The number of replans in this plan's sequence, starting at 0. | Integer |
| `deployment` | A reference to the current deployment. | Object |

### Change summary objects

The change summary objects in `context.plan.changes` and `context.plan.component_changes` include the following fields:

| Field | Description | Type |
| --- | --- | --- |
| `add` | Number of resources to be added. | Integer |
| `change` | Number of resources to be changed. | Integer |
| `import` | Number of resources to be imported. | Integer |
| `remove` | Number of resources to be removed. | Integer |
| `total` | Total number of resource actions. | Integer |

### Deployment context

The `context.plan.deployment` object includes the following fields:

| Field | Description | Type |
| --- | --- | --- |
| `deployment_name` | The name of the current deployment. | String |

### Diagnostic message objects

The diagnostic message objects in `context.errors` and `context.warnings` include the following fields:

| Field | Description | Type |
| --- | --- | --- |
| `summary` | A brief summary of the diagnostic message. | String |
| `detail` | Detailed information about the diagnostic message. | String |

## Examples

The following examples demonstrate common use cases for `deployment_auto_approve` blocks.

### Auto-approve small changes

In the following example, an auto-approval rule automatically approves plans with fewer than 3 total changes:

```hcl
deployment_auto_approve "small_changes" {
  check {
    condition = context.plan.changes.total < 3
    reason    = "Plan contains too many changes for automatic approval."
  }
}
```

### Auto-approve when specific components unchanged

In the following example, an auto-approval rule automatically approves plans when the database component has no changes:

```hcl
deployment_auto_approve "no_database_changes" {
  check {
    condition = context.plan.component_changes["component.database"].total == 0
    reason    = "Database component has changes that require manual review."
  }
}
```

### Auto-approve successful operations only

In the following example, an auto-approval rule only approves plans when the operation succeeded:

```hcl
deployment_auto_approve "successful_plans" {
  check {
    condition = context.success == true
    reason    = "Operation failed and requires manual intervention."
  }
}
```

### Multiple conditions

In the following example, an auto-approval rule requires multiple conditions to pass:

```hcl
deployment_auto_approve "safe_production_changes" {
  check {
    condition = context.plan.changes.total < 5
    reason    = "Too many changes for automatic approval."
  }

  check {
    condition = context.plan.changes.remove == 0
    reason    = "Resource removal requires manual approval."
  }

  check {
    condition = context.plan.deployment.deployment_name != "production"
    reason    = "Production deployments require manual approval."
  }
}
```
