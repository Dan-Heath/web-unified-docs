---
page_title: store block reference for Terraform Stack deployments
description: Use the `store` block to define key-value secrets in your deployment configuration and reference them in deployment input variable values.
---

# `store` block reference

Use the `store` block to define key-value secrets in your deployment configuration and reference them in deployment input variable values.

## Background

The `store` block allows you to access external secret stores and variable sets from your deployment configuration. After defining a `store` block, you can reference specific values from that store in your deployment block input variables.

When you define a `store` block, you specify two labels: the store type and the store name. The store type defines where Terraform should look for credentials and how to decode them. Different store types have different configuration requirements and cannot share arguments.

## Configuration model

The `store` block supports the following arguments based on store type:

- [`store "<STORE_TYPE>" "<LABEL>"`](#store) &nbsp; block
  - **For `varset` store type:**
    - [`id`](#varset-id) &nbsp; string
    - [`category`](#varset-category) &nbsp; string

## Complete configuration

The configuration depends on the store type you choose.

### Variable set store

```hcl
store "varset" "<LABEL>" {
  id       = "<VARIABLE_SET_ID>"
  category = "<terraform|env>"
}
```

## Specification

A `store` block supports the following configuration.

### `store "<STORE_TYPE>" "<LABEL>"`

The `store` block requires two labels: the store type and a unique name for the store within your deployment configuration. Currently, only the `varset` store type is supported.

The store name can be any valid [identifier](/terraform/language/syntax/configuration#identifiers) and must be unique among all stores in the same deployment configuration file.

## `varset` store type

Use the `varset` store type to enable your Stacks to access [variable sets](/terraform/cloud-docs/workspaces/variables/managing-variables#variable-sets) in HCP Terraform. Your Stack must have access to the variable set you are targeting, meaning it must be globally available or assigned to the project containing your Stack.

### Arguments

The following arguments are supported for the `varset` store type:

| Argument | Description | Type | Required? |
| --- | --- | --- | --- |
| `id` | The external ID of the variable set you want to access. | String | Required |
| `category` | Specifies whether to use Terraform or environment variables from the variable set. | String | Required |

### `id` {#varset-id}

The `id` argument specifies the external ID of the variable set you want to access. You can find this ID in the HCP Terraform UI or by using the HCP Terraform API.

```hcl
store "varset" "secrets" {
  id       = "varset-abc123def456"
  category = "terraform"
}
```

#### Summary

- Data type: String
- Default: None
- Required: Yes

### `category` {#varset-category}

The `category` argument specifies whether to use Terraform variables or environment variables from the variable set. You must specify either `"terraform"` or `"env"` depending on the type of variables you want to access.

```hcl
store "varset" "terraform_vars" {
  id       = "varset-abc123def456"
  category = "terraform"
}

store "varset" "env_vars" {
  id       = "varset-abc123def456"
  category = "env"
}
```

Each `store` block can only access one category of variables. If you need to access both Terraform and environment variables from the same variable set, define two separate `store` blocks.

#### Summary

- Data type: String
- Default: None
- Required: Yes
- Valid values: `"terraform"`, `"env"`

## Accessing store values

You cannot access an entire store and must specifically access individual keys within that store. Use the syntax `store.<STORE_TYPE>.<STORE_NAME>.<VARIABLE_NAME>` to reference specific variables.

```hcl
store "varset" "api_keys" {
  id       = "varset-abc123"
  category = "terraform"
}

deployment "production" {
  inputs = {
    database_password = store.varset.api_keys.db_password
    api_key          = store.varset.api_keys.external_api_key
  }
}
```

## Examples

The following examples demonstrate common use cases for `store` blocks.

### Basic variable set access

In the following example, a store accesses Terraform variables from a variable set:

```hcl
store "varset" "secrets" {
  id       = "varset-abc123def456"
  category = "terraform"
}

deployment "production" {
  inputs = {
    database_password = store.varset.secrets.db_password
    api_key          = store.varset.secrets.api_key
  }
}
```

### Separate Terraform and environment variables

In the following example, two stores access different categories of variables from the same variable set:

```hcl
store "varset" "terraform_vars" {
  id       = "varset-abc123def456"
  category = "terraform"
}

store "varset" "env_vars" {
  id       = "varset-abc123def456"
  category = "env"
}

deployment "production" {
  inputs = {
    # From Terraform variables
    instance_count    = store.varset.terraform_vars.instance_count
    database_password = store.varset.terraform_vars.db_password

    # From environment variables
    log_level         = store.varset.env_vars.LOG_LEVEL
    debug_mode        = store.varset.env_vars.DEBUG_ENABLED
  }
}
```

### Multiple variable sets

In the following example, stores access variables from different variable sets:

```hcl
store "varset" "app_secrets" {
  id       = "varset-app123"
  category = "terraform"
}

store "varset" "infrastructure_config" {
  id       = "varset-infra456"
  category = "terraform"
}

deployment "production" {
  inputs = {
    # Application secrets
    api_key           = store.varset.app_secrets.external_api_key
    database_password = store.varset.app_secrets.db_password

    # Infrastructure configuration
    instance_type     = store.varset.infrastructure_config.prod_instance_type
    region           = store.varset.infrastructure_config.primary_region
  }
}
```

### Environment-specific stores

In the following example, different deployments use different variable sets:

```hcl
store "varset" "staging_secrets" {
  id       = "varset-staging123"
  category = "terraform"
}

store "varset" "production_secrets" {
  id       = "varset-prod456"
  category = "terraform"
}

deployment "staging" {
  inputs = {
    database_url = store.varset.staging_secrets.staging_db_url
    api_key     = store.varset.staging_secrets.staging_api_key
  }
}

deployment "production" {
  inputs = {
    database_url = store.varset.production_secrets.prod_db_url
    api_key     = store.varset.production_secrets.prod_api_key
  }
}
```
